#version 460

#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_buffer_reference : require
#extension GL_ARB_gpu_shader_int64 : require
#extension GL_EXT_shader_8bit_storage : require
#extension GL_EXT_control_flow_attributes : require
#extension GL_KHR_shader_subgroup_arithmetic : require
#extension GL_KHR_shader_subgroup_basic : require
#extension GL_KHR_shader_subgroup_ballot : require

#include <ecs/entity_buffer>
#include <renderer/debug/printf>
#include <renderer/instances>
#include <renderer/math>
#include <renderer/meshes/mesh_attributes>
#include <renderer/meshes/mesh_data>
#include <renderer/meshes/mesh_indices>
#include <renderer/meshes/mesh_table>
#include <renderer/transform>
#include <surfels/buffers/surfel_data_r>
#include <surfels/buffers/surfel_grid_r>
#include <surfels/surfel>
#include <visibility/visibility_buffer>
#include <visibility/visibility_utils>

const uint g_TileSize = OBLO_SUBGROUP_SIZE;

layout(std430, binding = 0) restrict writeonly buffer b_OutTileCoverage
{
    surfel_tile_data g_TileData[];
};

layout(binding = 10, rg32ui) uniform restrict readonly uimage2D t_InVisibilityBuffer;

layout(binding = 16) uniform b_CameraBuffer
{
    camera_buffer g_Camera;
};

layout(local_size_x = g_TileSize, local_size_y = g_TileSize, local_size_z = 1) in;

bool read_spawn_data(in ivec2 screenPos, in uvec2 resolution, out surfel_spawn_data spawnData, out vec3 positionWS);

uint find_lowest_within_subgroup(in float value)
{
    const float lowestCoverage = subgroupMin(value);
    const bool isBestCandidate = lowestCoverage == value;
    const uvec4 bestCandidateBallot = subgroupBallot(isBestCandidate);
    return subgroupBallotFindLSB(bestCandidateBallot);
}

shared surfel_tile_data g_GroupCandidates[OBLO_SUBGROUP_SIZE];

void main()
{
    const ivec2 screenPos = ivec2(gl_WorkGroupID.xy * gl_WorkGroupSize.xy + gl_LocalInvocationID.xy);
    const uvec2 resolution = imageSize(t_InVisibilityBuffer);

    const bool outOfScreenBounds = screenPos.x >= resolution.x || screenPos.y >= resolution.y;

    // When nothing is in the visibility buffer, we will fall back to the skybox. These pixels should not really matter
    // for the coverage, for now we assign an arbitrary coverage though.
    float coverage = NO_SURFELS_NEEDED;
    uint coverageSum = 0;
    uint pixelsWithFragments = 0;
    vec3 positionWS;
    vec3 normalWS;

    surfel_spawn_data spawnData;
    const bool hasGeometry = !outOfScreenBounds && read_spawn_data(screenPos, resolution, spawnData, positionWS);

    if (hasGeometry)
    {
        const surfel_grid_header gridHeader = g_SurfelGridHeader;

        const ivec3 cell = surfel_grid_find_cell(gridHeader, positionWS);

        // Search neighboring cells and accumulate overlapping surfels

        uint overlappingSurfels = 0;

        // if (debug_is_center(screenPos, resolution))
        // {
        //     const uint cellIndex = surfel_grid_cell_index(gridHeader, cell);
        //     // debugPrintfEXT("Tiling cell: [ %d %d %d ] (%u)\n", cell.x, cell.y, cell.z, cellIndex);
        // }

        [[unroll]] for (int x = -1; x <= 1; ++x)
        {
            [[unroll]] for (int y = -1; y <= 1; ++y)
            {
                [[unroll]] for (int z = -1; z <= 1; ++z)
                {
                    const ivec3 current = cell + ivec3(x, y, z);

                    if (surfel_grid_has_cell(gridHeader, current))
                    {
                        const uint cellIndex = surfel_grid_cell_index(gridHeader, current);
                        const surfel_grid_cell cellData = g_SurfelGridCells[cellIndex];

                        // TODO: Get all surfels and accumulate
                        const uint surfelsCount = min(cellData.surfelsCount, SURFEL_MAX_PER_CELL);

                        // if (debug_is_center(screenPos, resolution))
                        // {
                        //     if (surfelsCount != 0)
                        //     {
                        //         printf_vec3("[TILING] Position WS: ", positionWS);
                        //         printf_vec3("[TILING] Bounds Min: ", vec3(g_SurfelGridHeader.boundsMin));
                        //         printf_vec3("[TILING] Cell: ", vec3(current));
                        //         printf_uint("[TILING] Surfels count: ", surfelsCount);
                        //     }
                        // }

                        for (uint cellSurfelIndex = 0; cellSurfelIndex < surfelsCount; ++cellSurfelIndex)
                        {
                            const uint surfelId = cellData.surfels[cellSurfelIndex];

                            const surfel_data surfel = g_SurfelData[surfelId];

                            // debug_assert(surfel.alive);

                            // if (debug_is_center(screenPos, resolution))
                            // {
                            //     printf_uint("[TILING] Surfel ID: ", surfelId);
                            //     printf_uint("[TILING] Surfel alive: ", uint(surfel.alive));
                            //     printf_vec3("[TILING] Pixel WS position: ", positionWS);
                            //     printf_vec3("[TILING] Surfel position: ", surfel.position);
                            // }

                            // We should instead probably weigh by distance, but for now we just add 1 if it's in range
                            overlappingSurfels += uint(surfel_affects(surfel, positionWS));
                        }
                    }
                }
            }

            coverageSum += overlappingSurfels;
            ++pixelsWithFragments;
        }

        if (debug_is_center(screenPos, resolution))
        {
            printf_uint("[TILING] Affecting surfels: ", overlappingSurfels);
        }
    }

    // We don't need to spawn surfels if the tile only has skybox
    coverage = pixelsWithFragments == 0 ? NO_SURFELS_NEEDED : float(coverageSum) / pixelsWithFragments;

    const uint bestSubgroupCandidateIdx = find_lowest_within_subgroup(coverage);

    if (gl_SubgroupInvocationID == bestSubgroupCandidateIdx)
    {
        surfel_tile_data tileData;
        tileData.coverage = coverage;
        tileData.spawnData = spawnData;
        const uint localOutputIndex = gl_LocalInvocationID.y;
        g_GroupCandidates[localOutputIndex] = tileData;
    }

    subgroupMemoryBarrier();
    memoryBarrierShared();
    barrier();

    // We only keep the first subgroup active now, to perform another round of reduction
    if (gl_LocalInvocationID.y > 0)
    {
        return;
    }

    const surfel_tile_data tileData = g_GroupCandidates[gl_SubgroupInvocationID];
    const uint bestGroupCandidateIdx = find_lowest_within_subgroup(tileData.coverage);

    if (gl_SubgroupInvocationID == bestGroupCandidateIdx)
    {
        const uint globalIndex = gl_WorkGroupID.y * gl_NumWorkGroups.x + gl_WorkGroupID.x;
        g_TileData[globalIndex] = tileData;

        // #if OBLO_DEBUG_PRINTF
        //         debugPrintfEXT("TIle %d %d -> %f\n", gl_WorkGroupID.y, gl_WorkGroupID.x, tileData.coverage);
        // #endif
    }
}

bool read_spawn_data(in ivec2 screenPos, in uvec2 resolution, out surfel_spawn_data spawnData, out vec3 positionWS)
{
    // Parse the visibility buffer to find which triangle we are processing
    const uvec4 visBufferData = imageLoad(t_InVisibilityBuffer, screenPos);

    visibility_buffer_data vb;

    if (!visibility_buffer_parse(visBufferData.xy, vb))
    {
        return false;
    }

    // Read the instance data we need
    const mesh_handle mesh = OBLO_INSTANCE_DATA(vb.instanceTableId, i_MeshHandles, vb.instanceId);
    const transform transform = OBLO_INSTANCE_DATA(vb.instanceTableId, i_TransformBuffer, vb.instanceId);

    triangle triangleWS;

    // Read the mesh data
    const mesh_table meshTable = mesh_table_fetch(mesh);
    const uvec3 vertexIndices = mesh_get_meshlet_indices(meshTable, mesh, vb.meshletId, vb.meshletTriangleId);

    // Read the vertex data and transform everything in world space, we do lighting calculation in that space
    [[unroll]] for (uint i = 0; i < 3; ++i)
    {
        const uint vertexId = vertexIndices[i];

        const vec3 vertexPosition = mesh_get_position(meshTable, vertexId);
        triangleWS.v[i] = (transform.localToWorld * vec4(vertexPosition, 1)).xyz;
    }

    barycentric_coords bc;

    // Cast a ray from the camera to the near plane and calculate the distance of the ray hit to the plane on the
    // triangle in world space, we use that to derive the position in world space
    const vec2 ndc = screen_to_ndc(screenPos, resolution);
    const ray cameraRay = visibility_calculate_camera_ray(g_Camera, ndc);

    float intersectionDistance;

    // Really the plan should be hitting here, since we already know the triangle was rendered by the rasterizer, we
    // mostly want to calculate at what distance it does
    if (!distance_from_triangle_plane(cameraRay, triangleWS, intersectionDistance))
    {
        return false;
    }

    positionWS = ray_point_at(cameraRay, intersectionDistance);
    barycentric_calculate(bc, triangleWS.v, positionWS);

    const ecs_entity entity = OBLO_INSTANCE_DATA(vb.instanceTableId, i_EntityIdBuffer, vb.instanceId);

    spawnData.entity = entity;
    spawnData.packedMeshletAndTriangleId = visibility_buffer_get_packed_meshlet_ids(visBufferData.xy);
    spawnData.barycentricU = bc.lambda.x;
    spawnData.barycentricV = bc.lambda.y;

    return all(greaterThanEqual(bc.lambda, vec3(0)));
}